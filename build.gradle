plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.ajoberstar.grgit' version '4.1.1'
    id 'java'
}

group 'me.jerriidesu'
version "0.1.0-dev+${getVersionMetadata()}"

repositories {
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.mojang:brigadier:1.0.18'
    implementation 'org.javacord:javacord:3.5.0'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
                'Multi-Release': 'true',
                'Main-Class': 'me.jerriidesu.musicbot.MusicBot'
        )
    }
}

processResources {
    filesMatching("version.txt") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()
}

def getVersionMetadata() {
    def build_id = System.getenv("GITHUB_RUN_NUMBER")

    // CI builds only
    if (build_id != null) {
        return "build.${build_id}"
    }

    if (grgit != null) {
        def head = grgit.head()
        def id = head.abbreviatedId

        // Flag the build if the build tree is not clean
        if (!grgit.status().clean) {
            id += "-dirty"
        }

        return "rev.${id}"
    }

    // No tracking information could be found about the build
    return "unknown"
}
